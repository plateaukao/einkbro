apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'com.google.devtools.ksp'

static def getCurrentTimestamp() {
    return new Date().format('MMddHHmm')
}

def showUpdateButton() {
    def value = project.getProperties().get("showUpdateButton")
    return value != null ? value : "false"
}

android {
    compileSdk 34
    buildToolsVersion = '34.0.0'

    defaultConfig {
        android.buildFeatures.buildConfig true

        applicationId "info.plateaukao.einkbro"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode 11_07_00
        versionName "11.7.0"

        buildConfigField("String", "builtDateTime", "\"${getCurrentTimestamp()}\"")
        buildConfigField("boolean", "showUpdateButton", showUpdateButton())

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }


    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.txt'
        }
        releaseDebuggable {
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.3'
    }

    lint {
        baseline = file("lint-baseline.xml")
        checkReleaseBuilds false
        disable 'MissingTranslation'
    }
    namespace 'info.plateaukao.einkbro'
}

dependencies {
    implementation 'com.google.android.material:material:1.11.0'

    // epublib
    implementation("com.positiondev.epublib:epublib-core:3.1") {
        exclude group: 'org.slf4j'
        exclude group: 'xmlpull'
    }
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'net.sf.kxml:kxml2:2.3.0' // 检查最新版本
    //

    // common lang
    implementation 'org.apache.commons:commons-text:1.9'

    // for epub saving: html processing
    implementation 'org.jsoup:jsoup:1.15.3'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation "androidx.room:room-runtime:2.6.1"
    implementation "androidx.room:room-ktx:2.6.1"
    ksp "androidx.room:room-compiler:2.6.1"

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation "androidx.compose.ui:ui:1.6.0"
    implementation 'androidx.navigation:navigation-runtime-ktx:2.7.6'
    implementation "androidx.navigation:navigation-compose:2.7.6"

    debugImplementation "androidx.compose.ui:ui-test-manifest:1.6.0"

    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'

    // for dark mode
    implementation 'androidx.webkit:webkit:1.10.0'

    def koin_version = "3.1.2"
    // Koin core features
    implementation "io.insert-koin:koin-core:$koin_version"
    // Koin test features
    testImplementation "io.insert-koin:koin-test:$koin_version"
    // Android
    implementation "io.insert-koin:koin-android-compat:$koin_version"

    // memory leak detection
    //debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

    // compose
    // Compose Material Design
    implementation 'androidx.compose.material:material:1.6.0'
    // Tooling support (Previews, etc.)
    debugImplementation 'androidx.compose.ui:ui-tooling:1.6.0'
    implementation "androidx.compose.ui:ui-tooling-preview:1.6.0"
    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.6.0'

    implementation "com.google.accompanist:accompanist-drawablepainter:0.31.0-alpha"

    // reorder lazylist
    implementation "org.burnoutcrew.composereorderable:reorderable:0.9.6"

    // okhttp
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'
    implementation 'com.squareup.okhttp3:okhttp-sse:4.11.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.5.0'
}
