apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 32
    buildToolsVersion '31.0.0'

    defaultConfig {
        applicationId "info.plateaukao.einkbro"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 90500
        versionName "9.5.0"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        releaseDebuggable {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    buildFeatures {
        viewBinding true
        // Enables Jetpack Compose
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.2.0-beta03'
    }

    lint {
        checkReleaseBuilds false
        disable 'MissingTranslation'
    }
}

dependencies {
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.preference:preference-ktx:1.2.0'

    // epublib
    implementation("com.positiondev.epublib:epublib-core:3.1") {
        exclude group: 'org.slf4j'
        exclude group: 'xmlpull'
    }
    implementation 'org.slf4j:slf4j-api:1.7.32'
    //

    // common lang
    implementation 'org.apache.commons:commons-text:1.9'

    // for epub saving: html processing
    implementation 'org.jsoup:jsoup:1.14.3'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    implementation "androidx.room:room-runtime:2.4.3"
    implementation "androidx.room:room-ktx:2.4.3"
    kapt 'org.xerial:sqlite-jdbc:3.36.0.2'
    kapt "androidx.room:room-compiler:2.4.3"

    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'

    // for dark mode
    implementation 'androidx.webkit:webkit:1.4.0'

    def koin_version = "3.1.2"
    // Koin core features
    implementation "io.insert-koin:koin-core:$koin_version"
    // Koin test features
    testImplementation "io.insert-koin:koin-test:$koin_version"
    // Android
    implementation "io.insert-koin:koin-android-compat:$koin_version"

    implementation 'androidx.activity:activity-ktx:1.5.1'

    // memory leak detection
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'

    // compose
    // Compose Material Design
    implementation 'androidx.compose.material:material:1.3.0-alpha02'
    // Tooling support (Previews, etc.)
    debugImplementation 'androidx.compose.ui:ui-tooling:1.3.0-alpha02'
    implementation "androidx.compose.ui:ui-tooling-preview:1.3.0-alpha02"
    // UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4:1.3.0-alpha02'

    // reorder lazylist
    implementation "org.burnoutcrew.composereorderable:reorderable:0.9.0"
}
